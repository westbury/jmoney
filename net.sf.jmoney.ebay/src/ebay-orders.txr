@(skip)
@{username}username, click for member’s profile ( @{feedbackscore}Feedback@{rest}
@(skip)
Orders
@(collect :maxgap 6)
Order date:@{orderdate /\d\d\s\w\w\w,\s\d\d\d\d/}
@(assert)
Order total:£@{ordertotal}
Order number:@{ordernumber}
View order details@{orderdescription}
@  (collect)
@{description1}@; once in a while this does not match description below. This has extra spaces
@  (maybe)
Dispatched: @{dispatchjunk}
@  (end)
@  (maybe)
Delivered on @{deliverydate}
@  (end)
@  (maybe)
Est. delivery @{estimateddeliverydaterange}
@  (end)
@  (maybe)
Return window closed on @{returnclosedate}.
@  (end)
@  (maybe)
Tracking number: @{trackingnumber}
@  (end)
@  (maybe)
The seller has refused to cancel this order. View details
@    (bind rejectedreturn true)
@  (end)
@  (maybe)
The seller has cancelled this order. View details
@    (bind ordercancelled true)
@  (end)
@{description}
@  (maybe)
Main Colour:: @{color}
@  (end)
@  (maybe)
Size: @{size}
@  (end)
£@{itemprice /\-?(\d,)?\d\d?\d?\.\d\d/}
@  (assert)
Sold by:
@  (maybe)
@{seller}username, click for member’s profile@; once in a while this line is missing
@  (end)
@(skip 5)
More actions
@  (until)
Order date:@{discardorderdate}
@  (end)
@(end)
