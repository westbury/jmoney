@(collect)
Order info
@(assert)
Time placed	@{orderdate /\d\d?\s\w\w\w\s\d\d\d\d/} at @{ordertime}
Order number	@{ordernumber}
Total	£@{total} (@{numberofitems})
Sold by	@{seller}
Delivery info
@(maybe)
Delivered on @{deliverydate /\w\w\w,\s\d\d?\s\w\w\w\s\d\d\d\d/}
@(end)

Paid
@{dayofyearpaid /\d\d?\s\w\w\w/}
@(maybe)
2
@(end)
Dispatched
@(maybe)
@{dayofyearshipped /\d\d?\s\w\w\w/}
@(or)
3
@(end)
Delivered
@(maybe)
@{dayofyeardelivered /\d\d?\s\w\w\w/}
@(end)
Tracking details
@(maybe)
Postal service	@{shippingservice}
Courier	@{carrier}
@(end)
@(maybe)
Number	@{trackingnumber}
@(end)
Item details
@(collect)
@{description}
@{description}

£@{amount}Unit price £@{unitprice}
@(assert)

Item number:@{itemnumber}

@(until)
@  (cases)
Order info
@  (or)
Related sponsored items
@  (or)
Sponsored items based on your recent views
@  (or)
Delivery address
@  (end)
@(end)
@(until)
@  (cases)
Related sponsored items
@  (or)
Sponsored items based on your recent views
@  (or)
Delivery address
@  (end)
@(end)
@(cases)
Related sponsored items
@(or)
Sponsored items based on your recent views
@  (or)
Delivery address
@(end)
@(skip)
Payment info
@(assert)
Ending in @{lastfourdigits}@{cardtype /Visa|Master Card/} credit card ending in @{lastfourdigits}

@{nameoncard}

£@{totalforday}
@{orderdayofyear /\d\d?\s\w\w\w/} at @{ordertime}

@(maybe)
Ending in @{lastfourdigits2}@{cardtype2 /Visa|Master Card/} credit card ending in @{lastfourdigits}

@{nameoncard2}

-£@{totalforday2}
@{orderdayofyear2 /\d\d?\s\w\w\w/} at @{ordertime2}

@(end)
@{numberofitems2}
£@{itemtotal2}
@(maybe)
Item discount
-£@{discount}
@(end)
Postage
@{shipping}
@(maybe)
Amount paid
£@{amountpaid}
Total refunded
-£@{amountrefunded}
@(end)
Order total
£@{overalltotal}
How do you like our order details page?
