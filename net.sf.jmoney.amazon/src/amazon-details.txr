@(skip)
Order Details
@(assert)
Ordered on @orderdate  Order # @ordernumber
Invoice 
Delivery Address
Margaret Westbury
69 Barton Road
CAMBRIDGE, Cambs CB3 9LG
United Kingdom
@(maybe)
Change
@(end)
Payment Method
Visa/Delta/Electron **** @lastfourdigits
@(maybe)
Change
@(end)
@(maybe)
Apply gift card balance

Enter code
 Submit
Apply
@(end)
Order Summary
Item(s) Subtotal: £@subtotal
Postage & Packing: £@postageandpackaging
@(maybe)
Total Before VAT: £@totalbeforevat
VAT: £@vat
@(end)
Total: £@total
Grand Total: £@grandtotal
Transactions
@(collect :maxgap 2)
@  (cases)
Track package
@      (throw parseError "'Track package' found but prior lines were unmatched.")
@  (or)
Delivered @deliverydate
Parcel was handed to resident
Track package
@  (or)
Delivered @deliverydate
Parcel was handed to a receptionist
Track package
@  (or)
Arriving today
Out for delivery
Track package
@  (or)
Arriving @expecteddate
Pre-ordered
Track package
@  (or)
Dispatching now
Delivery estimate: @expecteddate
@  (or)
Now expected @expecteddate
Dispatched
Track package
@  (end)
@  (collect)
 
@description
@(assert)
@    (cases)
Sold by: @soldby Product question? Ask Seller
@    (or)
Sold by: @soldby
@    (or)
@author
Sold by: @soldby
@    (end)
@  (maybe)
Eligible for return until @returndeadline
@  (end)
@  (maybe)
Return window closed on @returndeadline
@  (end)
@  (maybe)
Add-on Item
@  (end)
£@itemamount
Condition: @condition
@  (maybe)
Add gift option
Release date: @releasedate
@  (end)
 Submit
Buy it again
@  (until)
Hide Order
@  (end)
@    (skip 6)
Hide Order
@(end)
