@(skip)
Order Details
@(assert)
Ordered on @orderdate  Order # @ordernumber
Invoice 
Delivery Address
@deliveryname
@deliveryaddress1
@deliveryaddress2
United Kingdom
@(maybe)
Change
@(end)
Payment Method
Visa/Delta/Electron **** @lastfourdigits
@(skip)
Order Summary
@(assert)
Item(s) Subtotal: £@subtotal
Postage & Packing: £@postageandpackaging
@(maybe)
Total Before VAT: £@totalbeforevat
VAT: £@vat
@(end)
Total: £@total
@(maybe)
Gift Card Amount: -£@giftcard
@(end)
@(maybe)
Promotion Applied: -£@promotion
@(end)
Grand Total: £@grandtotal
@(collect :maxgap 8)
@  (cases)
Track package
@      (throw parseError "'Track package' found but prior lines were unmatched.")
@  (or)
Delivered @deliverydate
Parcel was handed to resident
@  (or)
Delivered @deliverydate
Parcel was handed to a receptionist
@  (or)
Delivered @deliverydate
Your parcel was delivered
@  (or)
Arriving today
Out for delivery
@  (or)
Expected by @expecteddate
Dispatched
@  (or)
Arriving @expecteddate
Pre-ordered
@(bind isnotdispatched "true")
@  (or)
Arriving @expecteddate
Dispatched
@  (or)
Dispatching now
Delivery estimate: @expecteddate
@  (or)
Now expected @expecteddate
Dispatched
@  (or)
Not yet dispatched
Delivery estimate: @expecteddate
@  (end)
@  (maybe)
Track package
@  (end)
@  (collect :maxgap 0)
@    (cases)
 @{quantity /\d+/}
@      (assert)
@quantity of @description
@    (or)
 
@      (assert)
@description
@    (end)
@    (assert)
@    (cases)
Sold by: @soldby Product question? Ask Seller
@    (or)
Sold by: @soldby
@    (or)
@author
Sold by: @soldby
@    (end)
@  (maybe)
Eligible for return until @returndeadline
@  (end)
@  (maybe)
Return window closed on @returndeadline
@  (end)
@  (maybe)
Add-on Item
@  (end)
£@itemamount
Condition: @condition
@  (maybe)
Add gift option
@  (end)
@  (maybe)
Release date: @releasedate
@  (end)
 Submit
Buy it again
@  (until)
Hide Order
@  (end)
@    (skip 6)
Hide Order
@(end)
