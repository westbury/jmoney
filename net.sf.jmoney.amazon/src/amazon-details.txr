@(skip)
Order Details
@(assert)
Ordered on @orderdate  Order # @ordernumber
Invoice 
@(maybe)
 
@(end)
Delivery Address
@deliveryname
@deliveryaddress1
@deliveryaddress2
United Kingdom
@(maybe)
Change
@(end)
Payment Method
Visa/Delta/Electron **** @lastfourdigits
@(skip)
Order Summary
@(assert)
Item(s) Subtotal: £@subtotal
Postage & Packing: £@postageandpackaging
@(maybe)
Total Before VAT: £@totalbeforevat
VAT: £@vat
@(end)
Total: £@total
@(maybe)
Import Fees Deposit: £@importfeesdeposit
@(end)
@(maybe)
Gift Card Amount: -£@giftcard
@(end)
@(maybe)
Promotion Applied: -£@promotion
@(end)
Grand Total: £@grandtotal
@(maybe)
Refund Total £@refundtotal
@(end)
@(collect :maxgap 8 :mintimes 1)
@  (maybe)
@  (cases)
Track package
@      (throw parseError "'Track package' found but prior lines were unmatched.")
@  (or)
Delivered @deliverydate
Parcel was handed to resident
@  (or)
Delivered @deliverydate
Parcel was left in letterbox
@  (or)
Delivered @deliverydate
Parcel was handed to a receptionist
@  (or)
Delivered @deliverydate
Your parcel was delivered
@  (or)
Arriving today
Out for delivery
@  (or)
Expected by @expecteddate
Dispatched
@  (or)
Arriving @expecteddate
Pre-ordered
@(bind isnotdispatched "true")
@  (or)
Arriving @expecteddate
Dispatched
@  (or)
Dispatching now
Delivery estimate: @expecteddate
@  (or)
Now expected @expecteddate
Dispatched
@  (or)
Not yet dispatched
Delivery estimate: @expecteddate
@  (or)
Refunded
Your return is in transit. Your refund has been issued. When will I get my refund? 
Refund for this return £@refund
@(bind returned "true")
@  (or)
Return started
Your refund will be processed when we receive your item.
@(bind returned "true")@  (end)
@  (end)
@  (maybe)
Track package
@  (end)
@  (collect :maxgap 0 :mintimes 1)
@    (cases)
 @{quantity /\d+/}
@      (assert)
@quantity of @description
@    (or)
 
@      (assert)
@description
@    (end)
@    (assert)
@    (cases)
Sold by: @soldby Product question? Ask Seller
@    (or)
Sold by: @soldby
@    (or)
@author
Sold by: @soldby
@    (end)
@  (maybe)
Eligible for return until @returndeadline
@  (end)
@  (maybe)
Return window closed on @returndeadline
@  (end)
@  (maybe)
Add-on Item
@  (end)
£@itemamount
@  (maybe)
Includes Import Fees Deposit
@(bind overseas "true")
@  (end)
Condition: @condition
@  (maybe)
Add gift option
@  (end)
@  (maybe)
Release date: @releasedate
@  (end)
 Submit
Buy it again
@  (until)
Hide Order
@  (end)
@    (skip 6)
Hide Order
@(end)
