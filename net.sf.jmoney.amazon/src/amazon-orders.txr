@(collect)
ORDER PLACED
@(assert)
@date
TOTAL
£@totalamount
@(maybe)
DISPATCH TO
@recipientname
@(end)
ORDER # @ordernumber
Order Details  Invoice 
@  (collect :maxgap 2)
@   (cases)
 
@moviename
Amazon Video
Sold by: Amazon Instant Video Limited
Your Video Library
@   (or)
Redeemed
 
Birthday Packages - E-mail Amazon.co.uk Gift Voucher
Message:
@giftcardmessage
Amount Sent to Status
£@itemamount @recipient Redeemed
@(bind giftcardpurchase "true")
@   (or)
@    (cases)
Track package
@      (throw parseError "'Track package' found but prior lines were unmatched.")
@    (or)
	Arriving @expecteddate
Pre-ordered
@    (or)
Arriving today
Out for delivery
@(bind expecteddate "today")
@    (or)
Arriving @expecteddate
Dispatched
@    (or)
Arriving @expecteddate
Shipping today
@    (or)
Arriving @expecteddate
Preparing for Dispatch
@    (or)
Arriving @expecteddate
Pre-ordered
@    (or)
Delivered @deliverydate
Parcel was left in letterbox
@    (or)
Delivered @deliverydate
Parcel was handed directly to a neighbor
@    (or)
Delivered @deliverydate
Parcel was handed to resident
@    (or)
Delivered @deliverydate
Parcel was handed to a receptionist
@    (or)
Delivered @deliverydate
Parcel was delivered as per instructions
@    (or)
Delivered @deliverydate
Your parcel was delivered
@    (or)
Expected by @expecteddate
Dispatched
@    (or)
Now expected @expecteddate
Dispatched
@    (or)
Not yet dispatched
Delivery estimate: @expecteddate
@    (or)
Return complete
Your return is complete. Your refund has been issued. When will I get my refund? 
@(bind returned "true")
@    (or)
Refunded
Your return is in transit. Your refund has been issued. When will I get my refund? 
@(bind returned "true")
@    (end)
@    (maybe)
Track package
@    (end)
@    (collect :maxgap 0)
@      (cases)
 @{quantity /\d+/}
@        (assert)
@quantity of @description
@      (or)
 
@        (assert)
@description
@      (end)
@      (assert)
@      (cases)
Sold by: @soldby
@      (or)
@author
Sold by: @soldby
@      (end)
@      (maybe)
Eligible for return until @returndeadline
@      (end)
@      (maybe)
Return window closed on @returndeadline
@      (end)
@      (maybe)
Add-on Item
@      (end)
£@itemamount
@      (maybe)
Release date: @releasedate
@      (end)
@      (maybe)
Includes Import Fees Deposit
@(bind overseas "true")
@      (end)
@      (cases)
 Submit
Buy it again
@      (or)
Write a product review
@      (end)
@      (until)
Hide Order
@    (end)
@   (end)
@    (skip 6)
Hide Order
@    (until)
ORDER PLACED
@  (end)
@(end)
